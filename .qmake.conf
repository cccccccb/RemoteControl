REMOTECONTROL_ROOT_PROJECT_PWD=$${PWD}
REMOTECONTROL_QMAKE_CONFIG_PATH=$${REMOTECONTROL_ROOT_PROJECT_PWD}/qmake_config
REMOTECONTROL_THREEPARTY_PATH=$${REMOTECONTROL_ROOT_PROJECT_PWD}/3rdparty
REMOTECONTROL_DEST_BIN_PATH=$${REMOTECONTROL_ROOT_PROJECT_PWD}/bin

_TMP_QMAKE_SPEC=$$[QMAKE_SPEC]
contains(_TMP_QMAKE_SPEC,.*win32.*) {
    REMOTECONTROL_BUILD_ON_WINDOW=1
    REMOTECONTROL_THREEPARTY_INCLUDE_PATH=$${REMOTECONTROL_THREEPARTY_PATH}/include/windows
    REMOTECONTROL_THREEPARTY_LIBS_PATH=$${REMOTECONTROL_THREEPARTY_PATH}/libs/windows

    DEFINES+=UNICODE WIN32_LEAN_AND_MEAN NOMINMAX

    CONFIG(debug, debug|release) {
        QMAKE_CXXFLAGS += /W3 /MP
        QMAKE_CFLAGS += /W3 /MP        
        QMAKE_LFLAGS += /IGNORE:4099 /IGNORE:4251
    } else {
        QMAKE_CXXFLAGS += /W3 /MP
        QMAKE_CFLAGS += /W3 /MP
        QMAKE_LFLAGS += /DEBUG /OPT:REF /IGNORE:4099 /IGNORE:4251
    }
}

contains(_TMP_QMAKE_SPEC,.*linux.*) {
    REMOTECONTROL_BUILD_ON_LINUX=1
    REMOTECONTROL_THREEPARTY_INCLUDE_PATH=$${REMOTECONTROL_THREEPARTY_PATH}/include/linux
    REMOTECONTROL_THREEPARTY_LIBS_PATH=$${REMOTECONTROL_THREEPARTY_PATH}/libs/linux
}

QMAKEFEATURES=$${REMOTECONTROL_QMAKE_CONFIG_PATH}/features
REMOTECONTROL_VERSION_MAJOR=2
REMOTECONTROL_VERSION_MINOR=6
REMOTECONTROL_VERSION_PATCH=8

DEFINES+=REMOTECONTROL_VERSION_MAJOR=$${REMOTECONTROL_VERSION_MAJOR}
DEFINES+=REMOTECONTROL_VERSION_MINOR=$${REMOTECONTROL_VERSION_MINOR}
DEFINES+=REMOTECONTROL_VERSION_PATCH=$${REMOTECONTROL_VERSION_PATCH}
DEFINES+=GIT_COMMIT_COUNT=0

DEFINES+=USE_PCG_GENERATOR PROTOBUF_USE_DLLS ASIO_STANDALONE ASIO_NO_DEPRECATED
